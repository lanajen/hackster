if (typeof(Pusher) == 'function')
  client = new Pusher('<%= PUSHER_API_KEY %>')

  <% unless Rails.env == 'production' %>
  Pusher.log = (message) ->
    if (window.console && window.console.log)
      window.console.log(message)
  <% end %>

  scrollToLastMessage = ->
    $('.chat-inner').scrollTop($('.chat-inner ul').height())

  jQuery ->
    if $('#chat time').last().length
      time = Date.parse $('#chat time').last().attr('datetime')
    else
      time = new Date().getTime()
    lastMsgDate = new Date(0)
    lastMsgDate.setUTCMilliseconds time
    lastMsgDate = "#{lastMsgDate.getFullYear()}/#{lastMsgDate.getMonth()+1}/#{lastMsgDate.getDate()}"

    channelId = $('meta[name=channel_id]').attr('content')
    sub = client.subscribe('presence-group_' + channelId)

    sub.bind 'pusher:subscription_succeeded', (payload) ->
      $('textarea[name="chat_message[raw_body]"]').prop('disabled', false).focus()
      $('.participants-count').text(payload.count)
      $.each payload.members, (i, member) ->
        $('.chat-participants ul').append(member.tpl)

    sub.bind 'pusher:member_added', (member) ->
      $('#chat .messages').append("<li><a href='#{member.info.url}' target='_blank'>#{member.info.name}</a> joined.</li>")
      scrollToLastMessage()
      addParticipant(member)

    sub.bind 'pusher:member_removed', (member) ->
      $('#chat .messages').append("<li><a href='#{member.info.url}' target='_blank'>#{member.info.name}</a> left.</li>")
      scrollToLastMessage()
      removeParticipant(member)

    sub.bind 'new:message', (payload) ->
      <% unless Rails.env == 'production' %>console.log(payload)<% end %>
      if payload.tpl
        $.each payload.tpl, (i, tpl) ->
          # handle localized date
          if tpl.time
            date = new Date 0
            date.setUTCSeconds tpl.time
            dateString = "#{date.getFullYear()}/#{date.getMonth()+1}/#{date.getDate()}"
            if dateString > lastMsgDate
              lastMsgDate = dateString

              if date.toLocaleDateString()
                dateString = date.toLocaleDateString('nu', { year: 'numeric', month: 'long', day: '2-digit'})

              $('#chat .messages').append("<li class='date'>#{dateString}</li>")

          # process template
          $(tpl.target).append(tpl.content)
          scrollToLastMessage()

    client.connection.bind 'connected', ->
      $('textarea[name="chat_message[raw_body]"]').prop('disabled', false).focus()

    client.connection.bind 'connecting', ->
      $('textarea[name="chat_message[raw_body]"]').prop('disabled', true)

    addParticipant = (member) ->
      val = $('.participants-count').text()
      $('.participants-count').text(parseInt(val) + 1)
      $('.chat-participants ul').append(member.tpl)

    removeParticipant = (member) ->
      val = $('.participants-count').text()
      $('.participants-count').text(parseInt(val) - 1)
      $(".participant-#{member.id}").remove()

    $(document).ready ->
      scrollToLastMessage()

      showTime = (el) ->
        time = el.attr('datetime')
        epoch = Date.parse time
        date = new Date 0
        date.setUTCMilliseconds epoch
        hours = date.getHours()
        hours = '0' + hours if hours < 10
        minutes = date.getMinutes()
        minutes = '0' + minutes if minutes < 10
        fullTime = "#{hours}:#{minutes}"
        el.data 'processed', true
        el.text fullTime

      $('#new_chat_message textarea').on 'keypress', (event) ->
        if event.which == 13 && !event.shiftKey
          event.preventDefault()
          that = this
          body = $(this).val()
          $(this).prop('disabled', true)
          $.ajax
            url: $(this).parents("form").attr("action")
            type: 'POST'
            data:
              chat_message:
                raw_body: body

            success: (response) ->
              $(that).val("").css("height", 36).prop("disabled", false).focus()

      # auto adjust the height of
      $('#new_chat_message textarea').on 'keyup keydown', ->
        t = $(this);
        if t[0].scrollHeight > 36
          t.css('height', t[0].scrollHeight)
        else
          t.css('height', 36)

      $('a.toggle-participants').on 'click', (e) ->
        e.preventDefault()
        $('body').toggleClass('participants-open')

      # adjust time to user timezone
      $('#chat').on 'DOMNodeInserted', '.chat-message', (e) ->
        timeEl = $(this).find('time')
        showTime timeEl unless timeEl.data('processed') == true

      $('.chat-message time').each (i, el) ->
        showTime $(this)

      # $('#chat').on 'click', '.load-more', (e) ->
      #   e.preventDefault()
      #   $.get window.href,
      #     last_id: 100
      #   , (data) ->
      #     console.log(data)
      #   , 'json'